#+TITLE:  Initiation to Perl
#+AUTHOR: Jerome SANCHEZ
#+DATE: [2020-06-19 Fri]
#+TODO: TODO WORKINGON | DONE CANCEL
#+TODO: UPDATE MOVE EXPLAIN EXAMPLE |

* Presentation
** A scripting language
*** Dynamic language
    Not strong type (string, integer...)
    Inference typing (operations determine its data type)
    Simple and easy to program and understand 

*** No compilation require
    Interpreter read and translate the source code at runtime

** Regex
   Perl is strong for text handling and parsing
   Operator regex native to the language

** Modular
   Module for Namespace libs for avoid function name conflict
   Extend module to "object oriented model" with "bless"

** Ecosystem
   Lots of libraries.
   METACPAN

** Example

*** Running perl command line
#+begin_src sh :results output
perl -e 'print "hello !\n"'
#+end_src


*** Running perl from a file
#+begin_src perl :shebang "#!/usr/local/bin/perl" :results output
use strict;
use warnings;

# un commantaire
my $str="Hello";
print $str." World\n";

exit 0;
#+end_src


** Web ressources
*** Learn Perl org
    https://learn.perl.org/tutorials/

*** Interpreter on Web
    https://www.tutorialspoint.com/execute_perl_online.php

*** PerlDoc (language documentation)
    https://perldoc.perl.org/

* Syntax and Types
** Syntax
*** Values and variables
    You develop Perl programs to manipulate some kinds of data.
    The data can be either number, characters or more complex such as list.
    Data is held as value.
    The below examples are values:

#+begin_src perl :results output
10
20.2
"Perl syntax"
#+end_src
    
    To manipulate data such as changing, adding, etc., you need variables. 
    You use a variable to store a value and through the name of the variable, you can process the value.
    The following illustrates some variables in Perl:

#+begin_src perl :results output
$x = 10;
$y = 20;
$s = "Perl string";
#+end_src

*** Comments
    In Perl, a comment begins with a hash (#) character. Perl interpreter ignores comments at both compile time and runtime. 
    You use the comment to document the logic of your code. The code tells you what it does however comment provides information on why the code does so.
    Comment is very important and useful to you as a programmer in order to understand the code later on, as well as other developers who will maintain your code in the future.

#+begin_src perl :results output

# A comments start with '#'
$x = 10;
$s = "Perl string"; # perl string

#+end_src

*** Expressions and statements
**** Expressions
    In Perl, an expression is anything that returns a value. 
    The expression can be used in a larger expression or a statement.
    The expression can be a literal number, complex expression with operators, or a function call.

**** Statement
    A statement is made up of expressions. Statement is executed by Perl at run-time. Each Perl statement must end with a semicolon (;).

**** Examples
#+begin_src perl

$a=10; # Statement with 10 an expression that returns value of 10
$b=12; # Statement with 12 an expression that returns value of 12

$a + $b  # expression

$a + $b; # a statement

#+end_src 

*** Blocks
    A block is made up of statements wrapped in curly braces. You use blocks to organize statements in program. 

#+begin_src perl

{
     $a = 1;
     $a = $a + 1;
     print($a);
}

#+end_src

   Any variable declared inside a block has its own scope. 
   It means the variables declared inside a block only last as long as the block is executed.

*** Keywords
    Perl has many reserved words.
    https://learn.perl.org/docs/keywords.html

** Types
*** SCALARS
   A scalar represent :
   - an integer or a float
   - a char or a string
   - a pointer/reference
   - or undef

**** Numbers
***** Integer
      Integers are whole numbers that have no number after the decimal points i.e 10,  -20 or 100.
      In Perl, integers are often expressed as decimal integers, base 10.

#+begin_src perl :results output

my $x = 20;
my $y = 100;
my $z = -200;
my $a = 123_763_213; # represent number : 123763213

my $number_decimal = 12;
my $number_binary  = 0b1100;
my $number_octal   = 014;
my $number_hex     = 0xC;

#+end_src

***** Floating-point 
      You use floating-point numbers to store real numbers. Perl represents floating-point numbers in two forms:
      - Fixed point: 100.25
      - Scientific:  +1.0025e2
#+begin_src perl :results output

my $x = 100.25;
my $y = +1.0025e2;

#+end_src

**** String
     In Perl, a string is a sequence of characters surrounded by some kinds of quotation marks.
     A string can contain ASCII, UNICODE and escape sequences characters such as \n.

    #+begin_src perl :results output
    my $s = "Perl";

    my $str_static = 'a string $s without interpolation and escaping a single quote \' \n*';
    my $str_q      = q/a string $s without interpolation and a single quote ' \n*/;

    my $str_i        = "a string $s with interpolation and a single quote ' \n";
    my $str_qq       = qq/a string $s with interpolation and a single quote ' \n/;
    my $str_qq_delim = qq{a string $s with interpolation and a single quote ' \n};

    printf " %s\n %s", $str_q, $str_static;
    print  "\n";
    printf " %s %s %s",  $str_i, $str_qq, $str_qq_delim;
#+end_src

#+RESULTS:
:  a string $s without interpolation and a single quote ' \n*
:  a string $s without interpolation and escaping a single quote ' \n*
:  a string Perl with interpolation and a single quote ' 
:  a string Perl with interpolation and a single quote ' 
:  a string Perl with interpolation and a single quote ' 

**** Operator
    - some numerical operator: + - * / ++ -- += -= /= *= %= sin cos exp log abs sqrt
    - some string operator: . .= length chop chomp reverse ord chr lc uc substr index rindex
**** Exercices
    1. Add two value
      #+begin_src perl :results output
      my $un=1;
      my $deux=2;
      print $un + $deux;
      print "\n";
      #+end_src

      #+RESULTS:
      : 3

    2. Concat two strings
      #+begin_src perl :results output
      my $str1="Hello";
      my $str2="World!";
      print $str1." ".$str2."\n";
      #+end_src

      #+RESULTS:
      : Hello World
   
    3. Where is char 'e'
      #+begin_src perl :results output
      print index ("j'ai trouve le caractere",'e');
      #+end_src

*** List
    A Perl list is a sequence of scalar values.
    - List are IMMUTABLE! 
    - You use parenthesis and comma operators to construct a list.
    - Each value in the list is called list element.
    - List elements are indexed and ordered. 
    - You can refer to each element by its position.
    NB: a list can contain differents types of element.
#+begin_src
()                          # Empty list
(10, 20, 30)                # List of integers
("this", "is", "a", "list") # List of strings

(2020, "Mars", 10) # heterogene list
qw(2020 "Mars" 10) # an other form to represent a list

('a'..'d')  # Range list equivalent to ('a','b','c','d')

#+end_src

*** ARRAYS
   By definition, an array is a variable that provides dynamic storage for a list.
   - Arrays are MUTABLE
   - An array variable begins with an at-sign ( @)
   - Index of an array start from 0.

**** Affectation
    #+begin_src perl :shebang "#!/usr/local/bin/perl" :results output
    use warnings;
    use strict;

    my @empty = ();                       # Empty array from list
    my @t1 = (3,'chaine',"hello here"); # Copy a list to an array
    my ($a,$b,$c)=(3, 'string', "hello here"); # affectation of several values

    print " Array : @t1 (before)\n";
    $t1[1] = 4;                          # change second element
    print " Array : @t1 (after)\n";

    print "Copy array\n";
    my @t2 = (1,2);
    print " Array : @t1 (before)\n";
    @t1 = @t2;
    print " Array : @t1 (after)\n";
    my ($premier, $second)= @t1; # multiple assignement

    my $count_t2 = scalar @t2; # get the number of element into an array
    print 'Size of @t2 = '.$count_t2."\n";

    # Remove and append element at the head
    my @simple_array = (1,2,3);
    print "Simple array = @simple_array \t shift \@simple_array = ".(shift @simple_array)." \t Simple array = @simple_array \n";
    @simple_array = (1,2,3);
    print "Simple array = @simple_array \t unshift \@simple_array,(-1,0) = ".(unshift @simple_array, (-1,0))." \t Simple array = @simple_array \n";

    # Remove and append element at the tail
    my @simple_array = (1,2,3);
    print "Simple array = @simple_array \t pop \@simple_array = ".(pop @simple_array)." \t Simple array = @simple_array \n";
    @simple_array = (1,2,3);
    print "Simple array = @simple_array \t push \@simple_array,(4,5) = ".(push @simple_array, (4,5))." \t Simple array = @simple_array \n";

    #+end_src

    #+RESULTS:
    #+begin_example
     Array : 3 chaine hello here (before)
     Array : 3 4 hello here (after)
    Copy array
     Array : 3 4 hello here (before)
     Array : 1 2 (after)
    Size of @t2 = 2
    Simple array = 1 2 3 	 shift @simple_array = 1 	 Simple array = 2 3 
    Simple array = 1 2 3 	 unshift @simple_array,(-1,0) = 5 	 Simple array = -1 0 1 2 3 
    Simple array = 1 2 3 	 pop @simple_array = 3 	 Simple array = 1 2 
    Simple array = 1 2 3 	 push @simple_array,(4,5) = 5 	 Simple array = 1 2 3 4 5 
    #+end_example

**** Operator
    scalar
    join split
    sort grep map  
    shift unshift push pop

*** HASHES
    A Perl hash is defined by key-value pairs.
    Perl stores elements of a hash in such an optimal way that you can look up its values based on keys very fast.
    You must use descriptive keys to access hash’s element.
    A hash is sometimes referred to as an associative array.
    A hash variable must begin with a percent sign (%)
    Each Key in hash should be unique or else it will override your value, which was assigned previously.
**** Affectation
   #+begin_src perl :results output
   my %h; # undef hash
   my %h2 = (); # empty hash
   my %h3 = ( "Paul" => "01.23.45.67.89",
          "Virginie" => "06.06.06.06.06",
          "Pierre" => "unknow ..." );

   my @t = ("Paul", "01.23.45.67.89", "Virginie",
         "06.06.06.06.06", "Pierre", "unknow ...");
   my %h4 = @t; # cast a array to a hash

   # Get and change a value
   my $valeur_paul=$h3{'Paul'};
   $h3{'Paul'}="01.23.45.67.00";
   print "Phone= ".$h3{'Paul'}."\n";

   # Add a new key/value pair
   $h3{'Arnaud'}="09.87.65.43.21";

   # Remove a single key/value pair
   delete $h3{'Paul'};

   my @array_keys = keys %h3;

   #+end_src

**** Operator
    keys values each
    exists delete

* Flow control
** If-then-else statements

   #+begin_src perl :results output
   my $age=35;
   if ($age < 18 ) {
     # Block
     print "Fruit juice\n";
   }
   elsif ( $age > 85) {
     # Block
     print "A tea\n";
   }
   else {
     # Block
     print "a beer\n";
   }

   print "if true\n" if (1);
   print "unless false\n" unless (0);

   #+end_src

   #+RESULTS:
   : a beer
   : if true
   : unless false

*** Tips
   - 0, '' (empty string), and undef are false

** Loop
*** for
    The Perl *for* loop statement allows you to loop over elements of a list.
    In each iteration, you can process each element of the list separately.
    This is why the *for* loop statement is sometimes referred to as *foreach* loop.

    In Perl, the *for* and *foreach* loop are interchangeable, therefore, you can use the *foreach* keyword in where you use the *for* keyword.

#+begin_src perl
    # Template form for for and foreach statement
    LABEL for (EXPR; EXPR; EXPR) BLOCK  # usefull
    LABEL foreach VAR (LIST) BLOCK # usefull
#+end_src

#+begin_src perl :results output
   # for C-like style
   for( my $i=0; $i<=20; $i+=2 ) {
     print "$i\n";
   }
#+end_src

   #+begin_src perl :results output
   my @a = ('a','b','c','d');
   foreach my $index (@a) {
     print "$a[$index]\n";
   }
   #+end_src

   #+begin_src perl :results output

   my %h = (1=>'a',2=>'b',3=>'c',4=>'d');
   
   print "All keys = ".join(', ',sort keys %h);
   print "\n";
   
   foreach my $k (sort keys %h) {
     print "value = $h{$k}\n";
   }
   #+end_src

*** while
   The Perl while loop statement executes a code block repeatedly as long as the test condition remains true.
   The test condition is checked at the beginning of each iteration.
   
   If the condition evaluates to true, the code block inside while loop executes. 
   At the beginning of each iteration, the condition is reevaluated.
   The loop is terminated if the condition evaluates to false.

#+begin_src perl
    # Template from for while statement
    LABEL while (EXPR) BLOCK
    LABEL while (EXPR) BLOCK continue BLOCK

    LABEL do BLOCK while (EXPR);

    LABEL until (EXPR) BLOCK
    LABEL until (EXPR) BLOCK continue BLOCK
#+end_src

   #+begin_src perl :results output
   my $i = 0;
   while( $i <= 20 ) {
     print "$i\n";
     $i+=2;
   }
   #+end_src

   Unlike the while statement that checks the condition at the beginning of each iteration, the do while loop statement checks the condition at the end of each iteration.
   Because do while loop statement checks the condition at the end of each iteration, the code block inside the loop executes at least once.

   #+begin_src perl :results output
   my $i = 0;
   do {
     print "$i\n";
     $i+=2;
   } while( $i <= 20 );
   #+end_src

   #+begin_src perl :results output
   my %h = (1=>'a',2=>'b',3=>'c',4=>'d');

   # looping example with "each" operator
   print "Looping with each operator\n";
   while (my ($k,$v) = each %h) {
    print "Key = $k,\tValue=$v\n";   
    print "value using \%h = $h{$k}\n";
   }
   #+end_src
*** Break a loop
   - next : next element
   - last : quit 
   - redo : reprocess 
   - goto : jump to a label (beware it's could injure your health mentality)

#+begin_src perl :results output
   # Example 
   my $i = 0;
   my %h=(1 => 'a');
   LABEL: 
       while( $i <= 20 ) {
        print "$i\n";
        LABEL_HASH: foreach my $k (keys %h) {
            if ($i > 15) {
              print "Here with i = $i\n";
              $i+=2;
              goto LABEL;
            }
            print " keys = $k\t Value=$h{$k}\n";
        }
        $i+=2;
       }
#+end_src

#+RESULTS:
#+begin_example
0
 keys = 1	 Value=a
2
 keys = 1	 Value=a
4
 keys = 1	 Value=a
6
 keys = 1	 Value=a
8
 keys = 1	 Value=a
10
 keys = 1	 Value=a
12
 keys = 1	 Value=a
14
 keys = 1	 Value=a
16
Here with i = 16
18
Here with i = 18
20
Here with i = 20
#+end_example


#+begin_src perl :results output
my $already_work=0; # prevent infinite loop when $i equals 4, because we redo it without incrementation
for( my $i=0; $i<=20; $i++ ) {
 if ($i == 4 && !$already_work) {
     $already_work=1;
     print "$i\n";
     redo;
 }
 next if ($i %2); # only even
 last if ($i>15); # quit if $i>15
 print "$i\n";
}

print "already_work=$already_work\n";
#+end_src

#+RESULTS:
#+begin_example
0
2
4
4
6
8
10
12
14
already_work=1
#+end_example


*** Exercises
   - Print all results for (1..9) * 7
#+begin_src perl :results output
#+end_src
   - Display the numbers from 1 to 10 and say if the number is odd or even.
#+begin_src perl :results output
#+end_src
   - Display prime number between 1 and 100
#+begin_src perl :results output
#+end_src

* Subroutines
  A subroutine is a block of code that can be reusable across programs.
  Perl subroutine is very flexible and powerful. You can define a subroutine anywhere in your program. 
  If you have subroutines defined in another file, you can load them in your program by using the *use*, *do* or *require* statement.
  You can call a subroutine directly or indirectly via a reference, a variable or an object.
  Perl also allows you to create anonymous subroutines that can be accessible through references.

** Syntax
   To define a subroutine, you use the following syntax:
#+begin_src perl
   sub NAME PROTOTYPES ATTRIBUTES BLOCK
#+end_src

   For IAI context, we use a lighter form to declare a subroutine
#+begin_src perl
   sub NAME  BLOCK
#+end_src

#+begin_src perl :results output
use warnings;
use strict;

sub say_something {
  print "Hi, this is a subroutine\n";
}

# Different ways to call a subroutine
&say_something;  # Optional '&'
say_something(); # explicitly call a subroutine
say_something;   # No parameters

#+end_src

#+RESULTS:
: Hi, this is a subroutine
: Hi, this is a subroutine
: Hi, this is a subroutine

  - The subroutines parameters are scalar only
  - The parameters are ordered and anonymous
  - You can access to the parameters through array : @_
  - Subroutine can *return* a scalar, a list, an array, a hash, a pointer, ...

#+begin_src perl :results output
use warnings;
use strict;

sub say_something {
  #@_
  my @a=@_;
  foreach my $v (@a) {
    print "Parameter = $v\n";  
  }
  
  print "Parameter first = $a[0]\n";
}

say_something (1,2,3);

#+end_src

#+RESULTS:
: Parameter = 1
: Parameter = 2
: Parameter = 3
: Parameter first = 1

** How-to write correctly a subroutine
  #+begin_src perl :results output

  sub display {
    my %params=@_;
    my $message=$params{-message};

    print "$message\n";
    return;
  }

  display(-message=>"bonjour");

  #+end_src

** Exercise
  1.  Write subroutine you_can_drink with parameter age.
    This subroutine return
    - "fruit juice" if age is lesser than 18,
    - "a tea" if age is greater than 85,
    - "a beer" if age between 18 and 84
    the main program display the result of you_can_drink(-age=>35)
#+begin_src perl :results output

#+end_src

* References
  A reference is a scalar variable that “points”  or refers to another object which can be a scalar, an array, a hash, etc.  
  A reference holds a memory address of the object that it points to.
  
  When a reference is dereferenced, you can manipulate data of the object that the reference refers to.
  The act of retrieving data through a reference is called dereferencing.

  Perl reference is like C pointer.
  Reference is an address memory to the data refered.
  A reference is a scalar. 

#+begin_src perl 

# Referencing some variables
my $ref_scal = \$scal;
my $ref_tab = \@tab;
my $ref_hash = \%hash;

# Dereferencing
my $scal = ${$ref_scal};
my @tab = @{$ref_tab};
my %hash = %{$ref_hash};

#+end_src

** Examples
*** Arrays
#+begin_src perl :results output
use strict;
use Data::Dumper; # lib to dump any perl data

  print "Static Array\n";

  print "1. Create an array\n";
  my @array= (0,1,2,3);
  print "   Array= @array \n";

  print "2. Change a value\n";
  $array[0]="I_change_a_value";
  print "   Array= @array \n";

  print "3. Get a value\n";
  my $a=$array[1];
  print "   a= $a\n";

  print "\nArray ref\n";

  print "1. Create an array ref\n";
  my $array_ref= [ 0,1,2,3 ];
  print "   Array_ref= $array_ref\n";
  print "   Array_ref= [", join(', ', @$array_ref)." ]\n";

  print "2. Change a value\n";
  $array_ref->[0]="I_change_a_value";
  print "   Array_ref= [", join(', ', @$array_ref)." ]\n";

  print "3. Get a value\n";
  my $a=$array_ref->[1];
  print "   a= $a\n";

  # Ref keyword
  print "   REF type=".(ref $array_ref)."\n";

exit 0;
#+end_src

*** Hash
#+begin_src perl :results output
use strict;
use Data::Dumper;


  print "Static Hash\n";
  print "1. Create a HASH";
  my %hash1= (k1=>'a', k2=>'b');
  my %hash2= ('k1','a', 'k2','b');
  print "   hash1 = %hash1 \n";
  print "   hash1= ";
  print "$_ => $hash1{$_}, " for (keys %hash1); print "\n";
  print "   hash2= ";
  print "$_ => $hash2{$_}, " for (keys %hash2); print "\n";

  print "2. Change a value\n";
  $hash1{k1} = 'c';
  $hash1{k3} = 'a';
  print "   hash1= ";
  print "$_ => $hash1{$_}, " for (keys %hash1); print "\n";

  print "3. Get a value\n";
  my $a= $hash1{k1};
  print "   a= $a\n";


  print "\n\nRef a Hash\n";
  print "1. Create a hash ref\n";
  my $hash1_ref={k1=>'a', k2=>'b'};
  my $hash2_ref={'k1','a', 'k2','b'};
  print "   hash1_ref= ";
  print "$_ => $hash1_ref->{$_}, " for (keys %$hash1_ref); print "\n";
  print "   hash2_ref= ";
  print "$_ => $hash2_ref->{$_}, " for (keys %$hash2_ref); print "\n";

  print "2. Change a hash ref\n";
  $hash1_ref->{k1} = 'c';
  $hash1_ref->{k3} = 'a';
  print "   hash1_ref= ";
  print "$_ => $hash1_ref->{$_}, " for (keys %$hash1_ref); print "\n";

  print "3. Delete a key/value pair\n";
  delete $hash1_ref->{k2};
  print "   hash1_ref= ";
  print "$_ => $hash1_ref->{$_}, " for (keys %$hash1_ref); print "\n";

  print "4. Get the value of a key\n";
  my $a= $hash1_ref->{k1};
  print "   a= $a\n";


  # Ref keyword
  print "   REF type=".(ref $hash_ref)."\n";

#+end_src

** Operators
   ref
   defined exists

* Modules
  Perl module is a reusable collection of related variables and subroutines that perform a set of programming tasks.
  Only 1 instance of the attributes module

#+begin_src sh :results output
# Some details from perl
perl -V
#+end_src

** Create a module

#+begin_src perl
package My::Module; # Package Name (::)

use strict;
use warnings;

use Text::CSV;  # you could use other package

# optionnaly if you want to use the function automatically (beware name conflict)
# use Exporter;
# our @EXPORT=qw(myfunc);

# Optionally we can control loading of a module
BEGIN {
    print "Dynamic loading module part\n";
}
END {
    print "Realasing module part\n";
}

# Declare a subroutine
sub myFunc {

    # 
    my ($param) = @_;

    # code
    # ...

    return;
}

1; # Very important ! don't forget it at the end of your package declaration

#+end_src

   Technically we create the file Module.pm in the directory My.
   And we can call them module function like this:

#+begin_src perl
 use My::Module;
 My::Module::myFunc(@params); # if not exported
#+end_src

#+begin_src perl
 use My::Module qw(myFunc); # with explicit name function we want
 myFunc(@params);
#+end_src

#+begin_src perl
 require My::Module;
 My::Module::myFunc(@params); # require don't have exported functions
#+end_src

** Use a module
#+begin_src perl :results output

use warnings;
use strict;

use JSON qw(encode_json decode_json); # load JSON.pm and use encode and decode subroutine

my @data=(1,2,3,4);
print "Json string of data = '".encode_json(\@data)."'\n";

my $debug=1;
if ($debug) {
  require Data::Dumper; # load Data/Dumper.pm only if $debug is "true"
  print "Data=".Data::Dumper::Dumper(\@data)."\n";
}

exit(0);

#+end_src

#+RESULTS:
: Json data=[1,2,3,4]
: Data=$VAR1 = [
:           1,
:           2,
:           3,
:           4
:         ];
: 


** Exercice
   Write a package with a function:  SUM(a,b) = a+b
   And write a main program that use the function SUM

* Object (blessing)
  There are three important rules in Perl object oriented programming:
  1) A class is a package.
  2) An object is a reference that knows its class.
  3) A method is a subroutine.

** Creation

#+begin_src perl :results output
package A;

sub new {
    my $self={};
    $self->{'attribut'}="A";
    print "Ref Self = ".ref($self)."\n";
    $self=bless($self, "A");
    print "Ref Self = ".ref($self)."\n";
    return $self;
}

sub dosomething {
    print "An output from A";
}
1; ## End of package A

package B;

sub new {
    my $self={};
    $self->{'attribut'}="B";
    print "Ref Self = ".ref($self)."\n";
    $self=bless($self, "B");
    print "Ref Self = ".ref($self)."\n";
    return $self;
}

sub dosomething {
    print "An output from B";
}
1; ## End of package B

use strict;
use warnings;
use Data::Dumper;

my $variable=A->new();
$variable->dosomething();
print "\nDump variable=".Dumper($variable)."\n";

#$variable=B->new();
bless($variable,"B");
$variable->dosomething();
print "\nDump variable=".Dumper($variable)."\n";

exit 0;

#+end_src

#+RESULTS:
#+begin_example
Ref Self = HASH
Ref Self = A
An output from A
Dump variable=$VAR1 = bless( {
                 'attribut' => 'A'
               }, 'A' );

An output from B
Dump variable=$VAR1 = bless( {
                 'attribut' => 'A'
               }, 'B' );

#+end_example

**** Example
#+begin_src perl :results output
package Vehicule;

use strict;
use warnings;

# for construct the object we use a method named "new()"
# usage: my $truck = Vehicule->new(6,'GREEN');
sub new {
  my ($class,$nbwheel,$color) = @_;

  my $this = {};
  $this->{NB_WHEEL} = $nbwheel;
  $this->{COLOR} = $color;

  # The built-in function bless is used to bless the reference to the class and return an instance of the class.
  return bless($this, $class);  # promote $this to an object
}

# fonction to change the color
# $v1->rePaint(-color=>'BLUE');
sub rePaint {
  my $this=shift;
  my %params=@_;

  $this->{COLOR} = $params{-color};
  return;
}

1;

##
# MAIN
##
use strict;
use warnings;

use Data::Dumper;

my $truck = Vehicule->new(6,'GREEN');
print  "##create truck=".Dumper($truck)."\n";

$truck->{COLOR}='BLUE';
$truck->{RECEPTION}=['TV','RADIO'];
print  "##change truck=".Dumper($truck)."\n";

$truck->rePaint(-color=>"BLACK");
print  "##repaint truck=".Dumper($truck)."\n";

exit 0;

#+end_src

#+RESULTS:
#+begin_example
##create truck=$VAR1 = bless( {
                 'NB_WHEEL' => 6,
                 'COLOR' => 'GREEN'
               }, 'Vehicule' );

##change truck=$VAR1 = bless( {
                 'RECEPTION' => [
                                  'TV',
                                  'RADIO'
                                ],
                 'NB_WHEEL' => 6,
                 'COLOR' => 'BLUE'
               }, 'Vehicule' );

##repaint truck=$VAR1 = bless( {
                 'RECEPTION' => [
                                  'TV',
                                  'RADIO'
                                ],
                 'NB_WHEEL' => 6,
                 'COLOR' => 'BLACK'
               }, 'Vehicule' );

#+end_example




*** Inheritance
#+begin_src perl :results output 
# --- fichier Velo.pm ---
package Velo;

use strict;
use warnings;

# use inheritance
use parent qw(Vehicule);

# other way to do it
#use Vehicule;
#our @ISA = qw(Vehicule);

sub new {
   my ($class,$color,$nbspeed) = @_;
   # $class is equal to "Velo"
   my $this = $class->SUPER::new( 2, $color );
   $this->{NB_SPEED} = $nbspeed;
   return bless($this,$class);
}

1;
#+end_src

** Exercise
  1. *easy* Implement a LIFO Stack (last info first out)
     we need the functions:
     - empty : stack is empty
     - push  : add an element
     - pop   : retrieve the last element
  2. *hard* Implement the Hanoi tower with 3 LIFO stack 
     
* Error Handling
  - Bacis tools:
    Warnings : warn
    Fatal error : die

  - Auxiliary Library *Carp* :

#+begin_src perl 
use Carp qw(cluck);

carp "Message";
cluck "Message";
croak "Message";
confess "Message";

#+end_src

    - Functions and impacts
      Fatal => exit program
      Stack => print the stack function call of the program

|         | Fatal | Stacktrace |
|---------+-------+------------|
| carp    | N     | N          |
| cluck   | N     | Y          |
| croak   | Y     | N          |
| confess | Y     | Y          |


  - Reserved word *eval* and message *$@*

    #+begin_src perl :results output
    eval {
      my $a=[1,2,3];
      die "I want to stop!!";
      print "A invisible message\n";
    };

    my $error = $@;
    if( $error ) {
      print "Error message: $error\n";
    }
    #+end_src

    #+RESULTS:
    : Error message: I want to stop!! at - line 3.

     
** Simulate Exception handling with Try::tiny
#+begin_src perl
use Try::Tiny;

try {
  print "some actions\n";
  die "An error occur";
}
catch {
  my $error = $_;
  print "Exception captured: $error\n";
}
; # don't forget ';' to the end of catch statement

print "I do other actions\n":
#+end_src


* Global standard variables
  [[https://perldoc.perl.org/perlvar.html][perlvar]] (english site)

  $_ current value
  @_ array of sub program parameter

#+begin_src perl :results output

foreach ('a'..'d') {
  print; # like: print $_;
  print "\n";
  print "With \$_ = ". $_ ."\n";
}

print "\n============\n";
sub f {
  print "Parameters = ".join(', ',@_)."\n";
  print "Parameter 2 = ".$_[1]."\n";
}

f(1,2,3,4);

#+end_src



  @ARGV command line parameter
#+begin_src shell :results output
perl -e 'foreach (@ARGV) { print $_; print ", ";}' 1 2 3 4
#+end_src

#+RESULTS:
: 1, 2, 3, 4, 

  $0 program name
  $$ current pid
  %ENV environment variables
  @INC path of libs perl

#+begin_src shell :results output
perl -e 'print "progname= $0 \t PID= $$ \t User= $ENV{USER} \nPerl path libs= @INC \n";'
#+end_src

#+RESULTS:
: progname= -e 	 PID= 11927 	 User= jsa 
:  Perl path libs= /usr/local/libdata/perl5/site_perl/i386-openbsd /usr/local/libdata/perl5/site_perl /usr/libdata/perl5/i386-openbsd /usr/libdata/perl5 

* REGular EXpressions
** Perl Regex Tutorial site
   [[https://perldoc.perl.org/perlretut.html][Perl Regex Tutorial]]
   [[https://perldoc.perl.org/perlrequick.html][Perl regular expressions quick start]]
   [[https://perldoc.perl.org/perlre.html][perlre - Perl regular expressions]]

   Testing regex site: https://regex101.com/

** Rules
   Qualifier
   Quantifier
   Grouping

** Frequentally used Regex operator
   =~
   !~ 
   m//  m%% m{} m() 
   s///  s%%% s{}{} s()()
   tr/// 
#+begin_src perl :results output
if ("text" =~ m/ex/) {
  print "text contains 'ex'\n";
}
my $test='a short text';
$test =~ s/short/long/;
print "Test = $test\n";
$test =~ tr/t/p/;
print "Test = $test\n";

#+end_src

#+RESULTS:
: text contains 'ex'
: Test = a long text
: Test = a long pexp


*** Regex like a variable
   my $maregex=qr//;
#+begin_src perl :results output
my $regex=qr(ex);

if ("text" =~ m/$regex/) {
  print "text contains $regex\n";
}
#+end_src

#+RESULTS:
: text contains (?^:ex)


** Capture Group 
    capturing value from regex

#+begin_src perl :results output
my @urls=( "www.google.com",
  "http://www.google.com",
  "http://www.google.com/",
  "http://www.google.com/api/search?q=1");

foreach my $url (@urls) {
  print "URL= $url\n";
  if ( $url =~ m%((http|https)://)?([\w|.|-]+)(/(.*)\?(.*))?% ) {
    my $proto=$1;
    print "Protocol= $1 - $2\n";
    print "FQDN= $3\n";
    print "Full Path= $4\n";
    print "Path = $5\n";
    print "Parameter = $6\n";
  }
  print "\n";
}

#+end_src

#+RESULTS:
#+begin_example
URL= www.google.com
Protocol=  - 
FQDN= www.google.com
Full Path= 
Path = 
Parameter = 

URL= http://www.google.com
Protocol= http:// - http
FQDN= www.google.com
Full Path= 
Path = 
Parameter = 

URL= http://www.google.com/
Protocol= http:// - http
FQDN= www.google.com
Full Path= 
Path = 
Parameter = 

URL= http://www.google.com/api/search?q=1
Protocol= http:// - http
FQDN= www.google.com
Full Path= /api/search?q=1
Path = api/search
Parameter = q=1

#+end_example

** Exercices

   |                                       |                                                                       |
   |---------------------------------------+-----------------------------------------------------------------------|
   | has a 'q'                             | has an 'a' but not 'e'                                                |
   | starts with a 'q'                     | has at least 2 consecutive vowels (a,e,i,o,u) like in the word "bear" |
   | has 'th'                              | has at least 3 vowels                                                 |
   | has an 'q' or a 'Q'                   | has at least 6 characters                                             |
   | has a '*' in it                       | has at exactly 6 characters                                           |
   | starts with an 'q' or an 'Q'          | all the words with either 'Bar' or 'Baz' in them                      |
   | has both 'a' and 'e' in it            | all the rows with either 'apple pie' or 'banana pie' in them          |
   | has an 'a' and somewhere later an 'e' | for each row print if it was apple or banana pie?                     |
   | does not have an 'a'                  | Bonus: Print if the same word appears twice in the same line          |
   | does not have an 'a' nor '            | Bonus: has a double character                                         |

* Files Handling
** Operator to check a file
   [[https://perldoc.perl.org/functions/-X.html][File test operato]]

#+begin_src perl :results output
my $file = "/usr/doc/perl";
if( -f $file && -w $file ) { print "This is a file and we can write it\n"; }
my $size = -s $file;
my $age = -M $file;
#+end_src
** Open, read, write, close
#+begin_src perl :results output

# All the finle into an array
open(my $fd,'<','data.txt') or die("open: $!");
# data.txt "a\nb"
# $all_file[0]='a';
# $all_file[1]='b';

my @all_file = <$fd>;
while( defined( $l = <$fd> ) ) {
   chomp $l;
   print "$. : $l\n";
}
close $fd;

# Line by line
open(my $fh, '<', 'data.txt') or warn "Can't open $arg: $!";
while ( ! eof($fh) ) {
    defined( $_ = readline $fh )
         or die "readline failed for $arg: $!";
            ...
}
close $fh;
#+end_src

   Write stream out *Perldoc print*
   File open mode *Perldoc open*
